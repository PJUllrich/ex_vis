# FigVis

```elixir
Mix.install(
  [
    {:req, "~> 0.3.9"},
    {:hammer, "~> 6.1"}
  ],
  config: [
    hammer: [
      backend:
        {Hammer.Backend.ETS, [expiry_ms: 60_000 * 60 * 4, cleanup_interval_ms: 60_000 * 10]}
    ]
  ]
)
```

## Section

```elixir
"mix xref graph --format dot"
```

## Utils

```elixir
defmodule Request do
  require Logger

  @max_requests_per_minute 900

  def get!(url) do
    build_request()
    |> check_rate_limit()
    |> Req.get!(url: url)
  end

  def post!(url, data) do
    build_request()
    |> check_rate_limit()
    |> Req.post!(url: url, json: data)
  end

  defp build_request() do
    access_token = access_token()

    Req.new(
      base_url: "https://api.miro.com/v2",
      headers: [
        authorization: "Bearer #{access_token}",
        accept: "application/json",
        content_type: "application/json"
      ]
    )
  end

  defp check_rate_limit(request) do
    case Hammer.check_rate("request", 60_000, @max_requests_per_minute) do
      {:allow, _count} ->
        request

      {:deny, _limit} ->
        Logger.info("Request Limit reached. Waiting 1s.")
        :timer.sleep(1_000)
        check_rate_limit(request)
    end
  end

  defp access_token() do
    System.get_env("LB_MIRO_ACCESS_TOKEN")
  end
end
```

## Read & Parse Dot File

```elixir
defmodule Note do
  defstruct [:id, :miro_id, :name, calls: []]
end

defmodule DotParser do
  def parse(filepath) do
    filepath
    |> File.read!()
    |> String.split("\n", trim: true)
    |> Enum.drop(1)
    |> Enum.map(&parse_line/1)
    |> Enum.reduce(%{}, fn line, notes ->
      case line do
        nil ->
          notes

        [calling_note_name, called_note_name] ->
          notes
          |> put_note(called_note_name)
          |> put_note(calling_note_name, called_note_name)

        [note] ->
          put_note(notes, note)
      end
    end)
    |> Map.values()
  end

  defp parse_line("digraph" <> _rest), do: nil
  defp parse_line("}"), do: nil

  defp parse_line(line) do
    line
    |> String.replace("\"", "")
    |> String.replace(" ", "")
    |> String.split("->", trim: true)
  end

  defp put_note(notes, note_name, called_note_name \\ nil)

  defp put_note(notes, note_name, nil) do
    {notes, _note} = get_or_create_note(notes, note_name)
    notes
  end

  defp put_note(notes, note_name, called_note_name) do
    {notes, note} = get_or_create_note(notes, note_name)
    note = put_call(note, called_note_name, notes)
    Map.put(notes, note_name, note)
  end

  defp get_or_create_note(notes, note_name) do
    case Map.get(notes, note_name) do
      nil ->
        new_note = %Note{id: map_size(notes), name: note_name, calls: []}
        notes = Map.put(notes, note_name, new_note)
        {notes, new_note}

      note ->
        {notes, note}
    end
  end

  defp put_call(%Note{calls: calls} = note, called_note_name, notes) do
    called_note = Map.fetch!(notes, called_note_name)
    Map.put(note, :calls, [called_note.id | calls])
  end
end
```

## Setup Miro Board

```elixir
defmodule Miro do
  @color %{
    gray: "gray",
    light_yellow: "light_yellow",
    yellow: "yellow",
    orange: "orange",
    light_green: "light_green",
    green: "green",
    dark_green: "dark_green",
    cyan: "cyan",
    light_pink: "light_pink",
    pink: "pink",
    violet: "violet",
    red: "red",
    light_blue: "light_blue",
    blue: "blue",
    dark_blue: "dark_blue",
    black: "black"
  }

  @hex_codes %{
    gray: "#F5F6F8",
    light_yellow: "#FFF9B1",
    yellow: "#F5D027",
    orange: "#FE9D47",
    light_green: "#D4F692",
    green: "#C9DE55",
    dark_green: "#93D174",
    cyan: "#67C6BF",
    light_pink: "#FECEDF",
    pink: "#F0929D",
    violet: "#EA94BA",
    red: "#F14725",
    light_blue: "#A6CCF5",
    blue: "#6CD7FA",
    dark_blue: "#9EA9FF",
    black: "#000000"
  }

  def color(), do: @color
  def hex_code(color), do: Map.fetch!(@hex_codes, color)

  def create_frame(board_id, name, pos_x, pos_y, frame_width, frame_height) do
    url = "/boards/#{board_id}/frames"

    data = %{
      "data" => %{
        "title" => name
      },
      "position" => %{
        "origin" => "center",
        "x" => pos_x,
        "y" => pos_y
      },
      "geometry" => %{
        "width" => frame_width,
        "height" => frame_height
      }
    }

    Request.post!(url, data)
  end

  def create_sticky_note(board_id, frame_miro_id, text, color, pos_x, pos_y, width) do
    url = "/boards/#{board_id}/sticky_notes"

    data = %{
      "data" => %{
        "content" => text,
        "shape" => "square"
      },
      "style" => %{
        "fillColor" => color
      },
      "position" => %{
        "origin" => "center",
        "x" => pos_x,
        "y" => pos_y
      },
      "geometry" => %{
        "width" => width
      },
      "parent" => %{
        "id" => frame_miro_id
      }
    }

    Request.post!(url, data)
  end

  def create_connector(board_id, start_item_id, end_item_id, color) do
    url = "/boards/#{board_id}/connectors"

    data = %{
      "startItem" => %{
        "id" => start_item_id
      },
      "endItem" => %{
        "id" => end_item_id
      },
      "style" => %{
        "strokeWidth" => "2",
        "strokeColor" => color
      }
    }

    Request.post!(url, data)
  end
end
```

```elixir
defmodule Grouper do
  def group(notes, groups) do
    groups_with_id =
      groups |> Enum.with_index(0) |> Enum.map(fn {group, id} -> {id, group} end) |> Map.new()

    group_keys = Enum.map(groups_with_id, fn {id, {regex, _color}} -> {id, regex} end)

    Enum.reduce(notes, %{}, fn note, groups ->
      matched_group =
        Enum.find(group_keys, fn {_group_id, regex} -> String.match?(note.name, regex) end)

      {matched_group_id, _regex} = matched_group
      prepend_node(groups, matched_group_id, note)
    end)
  end

  defp prepend_node(groups, group_id, node) do
    Map.update(groups, group_id, [node], fn nodes -> [node | nodes] end)
  end
end
```

```elixir
defmodule Frame do
  defstruct [
    :miro_id,
    :title,
    :color,
    :group_id,
    :notes,
    :connectors,
    :pos_x,
    :pos_y,
    :width,
    :height
  ]
end

defmodule Painter do
  # All measures are in pixel
  @note_width 200
  # Note Height is only assumed. Miro calculates the height
  # based on an aspect ratio depending on the width. Usually,
  # the height is 230 if the width is 200 though.
  @note_height 230
  @note_gap_x 30
  @note_gap_y 20

  @frame_width 2000
  @frame_gap_x 200
  @frame_gap_y 500

  # Max number of Frames to create vertically
  # before we create a new column of frames.
  @max_frame_count_y 3
  @max_note_count_x 8

  def create_all(board_id, grouped_notes, groups) do
    frames = create_frames(board_id, grouped_notes, groups)
    frames = create_notes(board_id, frames)
    create_connectors(board_id, frames)
  end

  def create_frames(board_id, grouped_notes, groups) do
    Enum.reduce(grouped_notes, {[], 0, 0}, fn {group_id, notes}, {frames, pos_x, pos_y} ->
      title = format_frame_title(groups, group_id)
      color = get_group_color(groups, group_id)
      frame_height = calc_frame_height(notes)
      miro_frame = Miro.create_frame(board_id, title, pos_x, pos_y, @frame_width, frame_height)

      miro_id = miro_frame.body["id"]

      frame = %Frame{
        miro_id: miro_id,
        group_id: group_id,
        color: color,
        notes: notes,
        title: title,
        pos_x: pos_x,
        pos_y: pos_y,
        width: @frame_width,
        height: frame_height
      }

      frames = [frame | frames]

      {next_pos_x, next_pos_y} =
        if vertical_frame_count_reached?(frames) do
          next_pos_x = pos_x + @frame_width + @frame_gap_x
          next_pos_y = 0
          {next_pos_x, next_pos_y}
        else
          {pos_x, pos_y + frame_height + @frame_gap_y}
        end

      {frames, next_pos_x, next_pos_y}
    end)
    |> elem(0)
  end

  def create_notes(board_id, frames) do
    Enum.map(frames, fn frame -> do_create_notes(board_id, frame) end)
  end

  defp do_create_notes(board_id, frame) do
    # Miro uses the top-left corner of the parent (frame)
    # to draw the sticky note. So, the position x: 0, y: 0
    # is exactly on the top-left corner.
    # So, we need to move the starting position down
    # by half a note.
    start_pos_x = div(@note_width, 2)
    start_pos_y = div(@note_height, 2) + @note_gap_y

    notes =
      Enum.reduce(frame.notes, {[], start_pos_x, start_pos_y}, fn note, {notes, pos_x, pos_y} ->
        miro_note =
          Miro.create_sticky_note(
            board_id,
            frame.miro_id,
            note.name,
            frame.color,
            pos_x,
            pos_y,
            @note_width
          )

        miro_id = miro_note.body["id"]
        note = Map.put(note, :miro_id, miro_id)
        notes = [note | notes]

        {next_pos_x, next_pos_y} =
          if horizontal_note_count_reached?(notes) do
            next_pos_x = start_pos_x
            next_pos_y = pos_y + @note_height + @note_gap_y
            {next_pos_x, next_pos_y}
          else
            next_pos_x = pos_x + @note_width + @note_gap_x
            next_pos_y = pos_y
            {next_pos_x, next_pos_y}
          end

        {notes, next_pos_x, next_pos_y}
      end)
      |> elem(0)

    Map.put(frame, :notes, notes)
  end

  def create_connectors(board_id, frames) do
    all_notes =
      frames
      |> Enum.reduce([], fn frame, acc -> frame.notes ++ acc end)
      |> Enum.map(fn note -> {note.id, note} end)
      |> Map.new()

    Enum.map(frames, fn frame ->
      connectors =
        for note <- frame.notes do
          do_create_connectors(board_id, frame, note, note.calls, all_notes)
        end

      Map.put(frame, :connectors, connectors)
    end)
  end

  defp do_create_connectors(_board_id, _frame, _note, [], _all_notes), do: :ok

  defp do_create_connectors(board_id, frame, note, calls, all_notes) do
    for call <- calls do
      end_note = Map.fetch!(all_notes, call)
      color = Miro.hex_code(frame.color)
      Miro.create_connector(board_id, note.miro_id, end_note.miro_id, color)
    end
  end

  defp format_frame_title(groups, group_id) do
    groups |> Enum.at(group_id) |> elem(0) |> Regex.source()
  end

  defp get_group_color(groups, group_id) do
    groups |> Enum.at(group_id) |> elem(1)
  end

  defp calc_frame_height(notes) do
    note_count = length(notes)
    row_count = ceil(note_count / @max_note_count_x)
    row_count * (@note_height + @note_gap_y * 2)
  end

  defp vertical_frame_count_reached?(frames) do
    length(frames) > 0 && rem(length(frames), @max_frame_count_y) == 0
  end

  defp horizontal_note_count_reached?(notes) do
    note_count = length(notes)
    note_count > 0 && rem(note_count, @max_note_count_x) == 0
  end
end
```

```elixir
board_id = "uXjVM_D3csY="
dot_file_path = "./Development/ex_vis/xref_graph.dot"

# Groups will be created in order. Modules will be put into the first group that
# matches their name. So, changing the order of the groups might change the group
# into which some modules fall.
groups = [
  {~r/tasks\//, :orange},
  {~r/test\//, :cyan},
  {~r/router*/, :green},
  {~r/channel*/, :blue},
  {~r/endpoint*/, :cyan},
  {~r/transports\//, :violet},
  {~r/socket*/, :red},
  {~r/mix\//, :yellow},
  # Required! 
  # The Default Group for all modules that don't match any other group.
  {~r/./, :gray}
]

notes = DotParser.parse(dot_file_path)
grouped_notes = Grouper.group(notes, groups)
```

```elixir
Painter.create_all(board_id, grouped_notes, groups)
```

<!-- livebook:{"offset":11602,"stamp":{"token":"QTEyOEdDTQ.wU5pAcMnWDXnBsLWTKp5JNfbqdZuICEHjztzTnENNKp5_DadaTOzbIpHmCo.g8E09fK3dnkRP1qu.sLIVraavphFc2jaGrQkBxVRFtX4S1e3-NqjgqED-zAVgSxjrUAjSSPg01xT4VpLmH3-oaKA.xbI5sPgzS-2ClBfIUOqHDA","version":1}} -->
